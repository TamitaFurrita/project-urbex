class Susan : TamiActorBase
{
	int hitCount;
	
	Default
	{
		Health 0xfffffffe;
		Radius 16;
		Height 80;
		Speed 8;
		Scale 0.28;
		PainChance 0;
		MeleeRange 44;
		RenderStyle "Translucent";
		Monster;
		+NOBLOOD +DONTTHRUST +FLOORCLIP -NODROPOFF
		Obituary "%o got %p neck snapped.";
		Tag "Susan.";
		//$Category "Monsters"
	}
	States
	{
	ReSpawn:
		SUSN AAAAAAAAAA 1 A_FadeTo(1.0, 0.1, FTF_CLAMP);
	Spawn:
		SUSN A 1 
		{
		A_Look();
		hitCount = 0;
		}
		Loop;
		
	See:
		SUSN A 5
		{
			if(target && target.Player) {
				playerInfo targetPlayer = target.Player;
				CVar targetFov = CVar.GetCVar("fov", targetPlayer);
				
				int fovInt;
				
				if(!targetFov) fovInt = 90;
				else fovInt = targetFov.GetInt();
				
				if(!CheckIfInTargetLOS(fovInt + 25)) {
					A_Chase("Melee", null);
					
					A_StartSound("susan/walk", 93, CHANF_NOSTOP, 1.0);
					A_StartSound("susan/breathe", 94, CHANF_NOSTOP, 0.5, 8);
					}
				else {
					vel = (0, 0, 0);
					A_StopSound(93);
					A_StopSound(94);
					}
				}
			if(target && !target.Player) {
			A_Chase("Melee", null);
			}
		}	
		TNT1 A 0;
		Loop;
	Melee:
		SUSN B 5 A_CustomMeleeAttack(target.Health * 4);
		Goto See;
	Death:
		SUSN BCDE 8;
		SUSN E 380;
		SUSN EEEEEEEEEEEEEEEEEEEEEEEEEEEEEEEEEEEEEEEEEEEEEEEEEEEEEEEEEEEEEEEEEEEEEEEEEEEEEEEEEEEEEEEEEEEEEEEEEEEE 1 {
		A_FadeTo(0.01, 0.01, FTF_CLAMP);
		}
		TNT1 A 0 {
			self.Health = 0xfffffffe;
			A_Teleport("Idle", "SusanSpot", flags: TF_NOFOG);
			}
		Goto ReSpawn;
	}
	
	override void Tick()
	{
		Super.Tick();
		double heartRate = 1;
		if (target) heartRate = Distance3D(target) / 128;
		heartRate = 1.0 - clamp(heartRate, 0, 1.0);
		heartRate = clamp(heartRate, 0, 0.7);
//		console.Printf("%f", heartRate);
		A_StartSound("susan/heart", 95, CHANF_NOSTOP, 0.5, 12, startTime: heartRate);
		if(!target)
		{
			A_StopSound(93);
			A_StopSound(94);
		}
	}
	
	override int TakeSpecialDamage(Actor inflictor, Actor source, int damage, Name damagetype)
	{
		hitCount++;
		
		if(hitCount == 10) {
			hitCount = 90;
			ResolveState("Death");
		}
		
		Return 0;
	}
}

class SusanSpot : SpecialSpot
{
  Default
  {
  +INVISIBLE
  }
}